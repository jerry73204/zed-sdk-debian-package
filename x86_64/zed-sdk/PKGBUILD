# Maintainer: Hsiang-Jui Lin <jerry73204@gmail.com>
pkgname=zed-sdk
pkgver=5.0.7
pkgrel=1
pkgdesc="StereoLabs ZED SDK for x86_64 (Desktop)"
arch=('amd64')
url="https://www.stereolabs.com/developers/release/"
license=('custom')

depends=(
  'libjpeg-turbo8'
  'libturbojpeg'
  'libusb-1.0-0'
  'libusb-1.0-0-dev'
  'libopenblas-dev'
  'libarchive-dev'
  'libv4l-0'
  'curl'
  'unzip'
  'zlib1g'
  'mesa-utils'
  # dev
  'libpng-dev'
  #
  'qtbase5-dev'
  'qtchooser'
  'qt5-qmake'
  'qtbase5-dev-tools'
  'libqt5opengl5'
  'libqt5svg5'
  # samples
  'libglew-dev'
  'freeglut3-dev'
  # AI dependencies (from NVIDIA apt repository)
  # ZED SDK 5.0 with TensorRT 8.6 and CUDA 12.1 (broader GPU compatibility)
  'libcudnn8>=8.9.7'
  'libnvinfer8>=8.6.1'
  'libnvinfer-plugin8>=8.6.1'
  'libnvonnxparsers8>=8.6.1'
)

optdepends=(
  'r!python3-pyzed: Python bindings for ZED SDK'
)

makedepends=(
  'zstd'
  'tar'
)
options=('!strip')
postinst='postinst.sh'
prerm='prerm.sh'
postrm='postrm.sh'

run_file="ZED_SDK_Ubuntu22_cuda12.1_tensorrt8.6_v${pkgver}.zstd.run"

source=(
    "${run_file}::https://stereolabs.sfo2.cdn.digitaloceanspaces.com/zedsdk/5.0/ZED_SDK_Ubuntu22_cuda12.1_tensorrt8.6_v${pkgver}.zstd.run"
    "python_shebang.patch"
    "zed_download_ai_models"
)

sha256sums=(
    '51790465ea257f6a47753ff2a751079588b52742615fad12ec6a04e8c5db3298'
    '1eed77b1cb24af3e58ecffde7a6bd1524215efeb9bafdc9364a2add2bc911fcd'
    'f4bff6ceb6de242615ddb2c305d70b35f7935adee4bbdda1d5d980a960efa09b'
)


prepare() {
  cd "${srcdir}"

  # Extract content from the self-extracting archive
  # The number 718 is the line where the binary data starts in the run file
  mkdir -p extract
  tail -n +718 "../${run_file}" | zstdcat -d | tar -xf - -C extract

  cd extract
  patch -Np1< "${srcdir}/python_shebang.patch"
}

package() {
  cd "${srcdir}/extract"

  # Create target directories
  mkdir -p "${pkgdir}/usr/local/zed"
  mkdir -p "${pkgdir}/etc/udev/rules.d"
  mkdir -p "${pkgdir}/etc/ld.so.conf.d"
  mkdir -p "${pkgdir}/usr/local/bin"
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"

  # Install license
  install -Dm644 "doc/license/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Install ZED libraries and includes
  cp -a -t "${pkgdir}/usr/local/zed" lib
  cp -a -t "${pkgdir}/usr/local/zed" include

  # Install firmware and resources
  cp -a -t "${pkgdir}/usr/local/zed" firmware
  # cp -a -t "${pkgdir}/usr/local/zed" resources

  # Note: dependencies/ai_dependencies/ is NOT installed
  # AI libraries (cuDNN 8.9.7, TensorRT 8.6.1) are provided by system packages
  # ZED SDK 5.0 uses dlopen() and will find them in /usr/lib/x86_64-linux-gnu/

  # Install cmake files
  install -Dm644 "zed-config.cmake" "${pkgdir}/usr/local/zed/zed-config.cmake"
  install -Dm644 "zed-config-version.cmake" "${pkgdir}/usr/local/zed/zed-config-version.cmake"

  # Install AI models download script
  install -Dm755 "${srcdir}/zed_download_ai_models" "${pkgdir}/usr/local/bin/zed_download_ai_models"

  # Install tools
  cp -a -t "${pkgdir}/usr/local/zed" tools

  # Create symlinks for tools
  find "${pkgdir}/usr/local/zed/tools/" -type f -executable | while read tool_exe; do
      name="$(basename $tool_exe)"
      ln -s "/usr/local/zed/tools/$name" "${pkgdir}/usr/local/bin/$(basename $tool_exe)"
  done

  # Install samples if available (optional)
  cp -a -t "${pkgdir}/usr/local/zed" samples

  # Install udev rules
  install -Dm644 "99-slabs.rules" "${pkgdir}/etc/udev/rules.d/99-slabs.rules"

  # Create ld.so.conf.d file (not bundled in archive, created by installer)
  mkdir -p "${pkgdir}/etc/ld.so.conf.d"
  echo "/usr/local/zed/lib" > "${pkgdir}/etc/ld.so.conf.d/zed.conf"

  # Create the doc directory
  cp -a -t "${pkgdir}/usr/local/zed" doc

  # Set base permissions for multi-user access
  # Note: Group ownership will be set to 'zed' in postinst.sh
  chmod -R 755 "${pkgdir}/usr/local/zed"

  # Make settings directory writable by group
  mkdir -p "${pkgdir}/usr/local/zed/settings"
  chmod 775 "${pkgdir}/usr/local/zed/settings"
}
